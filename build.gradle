plugins {
    id 'java'
    id 'io.qameta.allure' version "3.0.1"
}

def allureVersion = "2.32.0"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation "io.qameta.allure:allure-junit5:$allureVersion"
    testImplementation "io.qameta.allure:allure-selenide:$allureVersion"
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation 'com.codeborne:selenide:7.13.0'
    testImplementation 'net.datafaker:datafaker:2.5.3'
    testImplementation 'org.aeonbits.owner:owner:1.0.12'
}

test {
    useJUnitPlatform()
    systemProperties(System.getProperties())
}

task uiTest(type: Test) {
    useJUnitPlatform {
        def tag = System.getProperty("tag")
        // если тэг указан - запускаем ui + тэг тест
        // если нет - только ui
        if (tag != null && !tag.isBlank()) {
            includeTags("UI & ${tag}")
        } else {
            includeTags("UI")
        }
    }
    systemProperties(System.getProperties())
}

task apiTest(type: Test) {
    useJUnitPlatform {
        def tag = System.getProperty("tag")
        // если тэг указан - запускаем ui + тэг тест
        // если нет - только ui
        if (tag != null && !tag.isBlank()) {
            includeTags("API & ${tag}")
        } else {
            includeTags("API")
        }
    }
    systemProperties(System.getProperties())
}